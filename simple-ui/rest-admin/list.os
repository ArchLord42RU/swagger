// Предопределенная функция. Является точкой входа в обработку запроса.
//
Функция ОбработкаВызоваHTTPСервиса(Запрос) Экспорт

	Если Запрос.HTTPМетод = "GET" Тогда
		Возврат МетодОбработкиGET(Запрос);
	Иначе
		Возврат МетодОбработкиНеизвестен(Запрос);
	КонецЕсли;

КонецФункции

Функция МетодОбработкиGET(Запрос)

	РазделительПути = ПолучитьРазделительПути();

	// поиск файлов
	ТекущийКаталог = ТекущийСценарий().Каталог;
	КаталогФайлов = ОбъединитьПути(ТекущийКаталог, "Files");
	
	Файлы = НайтиФайлы(КаталогФайлов, "*.json", Истина);

	// анализ поиска
	ПереченьПроектов = Новый Соответствие;

	Для Каждого Файл Из Файлы Цикл

		Путь = Файл.Путь;

		ИмяПроекта = СтрЗаменить(СтрЗаменить(Путь, КаталогФайлов, ""), РазделительПути, "");

		ПереченьСервисов = ПереченьПроектов.Получить(ИмяПроекта);
		
		Если ПереченьСервисов = Неопределено Тогда
			ПереченьСервисов = Новый Массив;
		КонецЕсли;

		ИмяСервиса = Файл.ИмяБезРасширения;

		ПереченьСервисов.Добавить(ИмяСервиса);

		ПереченьПроектов.Вставить(ИмяПроекта, ПереченьСервисов);
	
	КонецЦикла;

	// сформируем ответ
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоМассива();

	Для Каждого КЗ Из ПереченьПроектов Цикл

		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		//
		ЗаписьJSON.ЗаписатьИмяСвойства("name");
		ЗаписьJSON.ЗаписатьЗначение(КЗ.Ключ);

		//
		ЗаписьJSON.ЗаписатьИмяСвойства("services");

		ЗаписьJSON.ЗаписатьНачалоМассива();

		Для Каждого ИмяСервиса ИЗ КЗ.Значение Цикл
			ЗаписьJSON.ЗаписатьЗначение(ИмяСервиса);
		КонецЦикла;

		ЗаписьJSON.ЗаписатьКонецМассива();

		ЗаписьJSON.ЗаписатьКонецОбъекта();

	КонецЦикла;

	ЗаписьJSON.ЗаписатьКонецМассива();

	Возврат ОтветУспешно(ЗаписьJSON.Закрыть());

КонецФункции 

Функция МетодОбработкиНеизвестен(Запрос)
	Возврат ОтветОшибка("Processing method unknown!");
КонецФункции

Функция ОтветОшибка(ТекстОшибки = "")
	
	Ответ = Новый HTTPСервисОтвет(400);
	Ответ.УстановитьТелоИзСтроки(СтандартноеТелоОтвета(Ложь, ТекстОшибки));

	Возврат Ответ;

КонецФункции

Функция ОтветУспешно(ТелоОтвета = Неопределено)
	
	Ответ = Новый HTTPСервисОтвет(200);

	Если ТелоОтвета = Неопределено Тогда
		Ответ.УстановитьТелоИзСтроки(СтандартноеТелоОтвета());
	Иначе
		Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	КонецЕсли;

	Возврат Ответ;

КонецФункции

Функция СтандартноеТелоОтвета(Успех = Истина, ТекстОшибки = "")
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("success");
	ЗаписьJSON.ЗаписатьЗначение(Успех);

	Если НЕ Успех Тогда
		
		ЗаписьJSON.ЗаписатьИмяСвойства("error_message");
		ЗаписьJSON.ЗаписатьЗначение(ТекстОшибки);

	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();

	Возврат ЗаписьJSON.Закрыть();

КонецФункции